grammar gpp.Example with org.eclipse.xtext.common.Terminals
hidden(WS, ML_COMMENT, SL_COMMENT) // 2 - Hidden terminals.


// 2 - Declaration of a output meta-model
generate example "http://www.gpp.cz/Example"


// 3 - Declaration of input meta-models 
import "classpath:/org/eclipse/xtext/Xtext.ecore" as xtext
import "http://www.eclipse.org/emf/2002/Ecore" as ecore


//  4 - Root parse rule 
Model:
	package = Package
	((imports += Import)* & class = Class)
;

terminal ID: '^'?('A'..'Z'|'_') ('A'..'Z'|'_'|'0'..'9')*;

Package: 'package' name=QualifiedName;

Import: 'import' className=QualifiedName;

QualifiedName returns ecore::EString:
	ID (',' ID)*
;

Class: (abstract?='abstract')? 'class' name=ID ('extends' superClass=[Class|ID])?
'{'
	(methods += Method | internalClasses += Class)*
'}';

Method: visibility=Modifier returnValue=[Class|ID] name=ID '(' (parameters+=Parameter (',' parameters+=Parameter)*)?  ')' '{' /* body */ '}';

Parameter: SpecificParameter name=ID;

SpecificParameter returns Parameter: IntParameter | StringParameter | ObjectParameter;

IntParameter: {IntParameter} 'int';

StringParameter: {StringParameter} 'string';

ObjectParameter: type=[Class|ID];

enum Modifier : public | protected | private;
